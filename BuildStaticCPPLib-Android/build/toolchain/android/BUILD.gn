import("//build/config/android/deployment_target.gni")

# Note: define a template named "android_toolchain"
template("android_toolchain") {
  toolchain(target_name) {
    assert(defined(invoker.toolchain_args),
           "Toolchains must declare toolchain_args")

    toolchain_args = {
      forward_variables_from(invoker.toolchain_args, "*")
    }

    # Note: turn on this line, if when debug GN
    #print("python3 sdk_info.py --target-cpu $current_cpu --target-environment $target_environment --deployment-target $ios_deployment_target")

    _sdk_info = exec_script("//build/config/android/scripts/sdk_info.py",
                            [
                              "--target-cpu",
                              current_cpu,
                              "--target-environment",
                              target_environment,
                              "--min-sdk-version",
                              min_sdk_version,
                            ],
                            "json")

    cc = "${_sdk_info.clang_path} --target=${_sdk_info.target} --sysroot=${_sdk_info.toolchain_path}/llvm/prebuilt/darwin-x86_64/sysroot"
    cxx = "${_sdk_info.clangcpp_path} --target=${_sdk_info.target} --sysroot=${_sdk_info.toolchain_path}/llvm/prebuilt/darwin-x86_64/sysroot"

    tool("link") {
      output = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
      rspfile = output + ".rsp"
      rspfile_content = "{{inputs_newline}}"

      outputs = [ output ]
      command = "$cxx {{ldflags}} -o $output -Wl,-filelist,$rspfile {{libs}} {{solibs}}"
      description = "LINK {{output}}"

      default_output_dir = "{{root_out_dir}}"
      default_output_extension = ""
      output_prefix = ""
    }

    tool("solink") {
      dylib = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
      rspfile = dylib + ".rsp"
      rspfile_content = "{{inputs_newline}}"

      LINK_FLAGS = ""
      LINK_LIBRARIES = "-lc++"

      outputs = [ dylib ]
      command = "$cxx {{ldflags}} $LINK_FLAGS $LINK_LIBRARIES -fpic -shared -o $dylib {{inputs}}"
      description = "SOLINK {{output}}"

      default_output_dir = "{{root_out_dir}}"
      default_output_extension = ".so"
      output_prefix = "lib"
    }

    tool("alink") {
      command = "rm -f {{output}} && ar rcs {{output}} {{inputs}}"
      description = "AR {{target_output_name}}{{output_extension}}"

      outputs =
          [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
      default_output_extension = ".a"
      output_prefix = "lib"
    }

    tool("cc") {
      depfile = "{{output}}.d"
      precompiled_header_type = "gcc"

      # Note: -MF must follow a file path for output, which is a dep file when specify -MMD, -MD, -MM, or -M
      command = "$cc -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
      depsformat = "gcc"
      description = "CC {{output}}"
      outputs = [ "{{target_out_dir}}/{{label_name}}/{{source_name_part}}.o" ]
    }

    tool("cxx") {
      depfile = "{{output}}.d"
      precompiled_header_type = "gcc"
      command = "$cxx -fPIC -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
      depsformat = "gcc"
      description = "CXX {{output}}"
      outputs = [ "{{target_out_dir}}/{{label_name}}/{{source_name_part}}.o" ]
    }

    tool("stamp") {
      command = "touch {{output}}"
      description = "STAMP {{output}}"
    }
  }
}

# Note: use `android_toolchain` template to define `clang_x86` toolchain
android_toolchain("clang_x86") {
  toolchain_args = {
    current_cpu = "x86"
    current_os = "android"
  }
}

# Note: use `android_toolchain` template to define `clang_x64` toolchain
android_toolchain("clang_x64") {
  toolchain_args = {
    current_cpu = "x64"
    current_os = "android"
  }
}

# Note: use `android_toolchain` template to define `clang_arm` toolchain
android_toolchain("clang_arm") {
  toolchain_args = {
    current_cpu = "arm"
    current_os = "android"
  }
}

# Note: use `android_toolchain` template to define `clang_arm64` toolchain
android_toolchain("clang_arm64") {
  toolchain_args = {
    current_cpu = "arm64"
    current_os = "android"
  }
}
